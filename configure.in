dnl
dnl $Id$
dnl
dnl Part of edelib.
dnl Copyright (c) 2005-2007 EDE Authors.
dnl
dnl This program is licenced under terms of the 
dnl GNU General Public Licence version 2 or newer.
dnl See COPYING for details.

m4_define([edelib_major_version], [2])
m4_define([edelib_minor_version], [0])
m4_define([edelib_patch_version], [0])
m4_define([edelib_version], [edelib_major_version.edelib_minor_version.edelib_patch_version])

AC_PREREQ(2.13)

AC_INIT(edelib, edelib_version, [karijes@users.sourceforge.net])

AC_CONFIG_SRCDIR(edelib/Nls.h)
AC_CONFIG_HEADER(edelib/_conf.h:edelib/_conf.h.in)

EDELIB_MAJOR_VERSION=edelib_major_version
EDELIB_MINOR_VERSION=edelib_minor_version
EDELIB_PATCH_VERSION=edelib_patch_version
EDELIB_API_VERSION=$PACKAGE_VERSION

dnl AC_CONFIG_AUX_DIRS(`pwd`/tools)

dnl startup variables
EDELIB_FLAGS="-Wall -pedantic"
EDELIB_DEBUG_FLAGS="-D_DEBUG"
EDELIB_OPTIM_FLAGS="-O2"

ALLFLAGS="-Wall -pedantic"
DEBUGFLAGS="-D_DEBUG"
OPTIMFLAGS="-O2"

dnl clear these since configure set it '-g -O2' by default
CFLAGS=
CXXFLAGS=

AC_PROG_CXX
AC_PROG_LN_S
AC_PATH_PROG(SED, sed)
AC_PATH_PROG(MSGFMT, msgfmt)
AC_PATH_PROG(XGETTEXT, xgettext)
AC_PATH_PROG(DOXYGEN, doxygen)

EDELIB_PROG_JAM

dnl basic headers
AC_HEADER_STDC
AC_CHECK_HEADER(string.h, AC_DEFINE(HAVE_STRING_H, 1, [Define to 1 if you have string.h file]))
AC_CHECK_FUNC(snprintf, AC_DEFINE(HAVE_SNPRINTF, 1, [Define to 1 if you have snprintf()]))
AC_CHECK_FUNC(vsnprintf, AC_DEFINE(HAVE_VSNPRINTF, 1, [Define to 1 if you have vsnprintf()]))

EDELIB_DATETIME
EDELIB_DEVELOPMENT

EDELIB_CHECK_X11
dnl user fired --without-x, we disable FLTK checks
if eval "test $have_x = yes"; then
	EDELIB_CHECK_FLTK
	dnl check for libXpm after FLTK since FLTKLIBS will contain Xpm library if found
	EDELIB_CHECK_LIBXPM
fi

EDELIB_NOTIFY
EDELIB_SHARED
EDELIB_NLS
EDELIB_MIME
EDELIB_CHECK_DBUS

AC_ARG_VAR([PKG_CONFIG], [pkg-config command])
AC_ARG_VAR([PKG_CONFIG_PATH], [A directory with .pc files for pkg-config])


if test -n "$OPTIMFLAGS"; then
	ALLFLAGS="$ALLFLAGS $OPTIMFLAGS"
else
	ALLFLAGS="$ALLFLAGS $DEBUGFLAGS"
fi

dnl construct needed variables for edelib-config
CFLAGS="$CFLAGS $ALLFLAGS $FLTKFLAGS $DSOFLAGS"
CXXFLAGS="$CXXFLAGS $ALLFLAGS $FLTKFLAGS $DSOFLAGS"
LDFLAGS="$LDFLAGS"
LDLIBS="$LDLIBS"

EDELIB_INIT_JAM

AC_SUBST(EDELIB_MAJOR_VERSION)
AC_SUBST(EDELIB_MINOR_VERSION)
AC_SUBST(EDELIB_PATCH_VERSION)
AC_SUBST(EDELIB_API_VERSION)
AC_SUBST(FLTKFLAGS)
AC_SUBST(FLTKLIBS)
AC_SUBST(FLTKLIBS_FULL)
AC_SUBST(DBUS_CFLAGS)
AC_SUBST(DBUS_LIBS)
AC_SUBST(SHARED)
AC_SUBST(LIBTOOL)
AC_SUBST(ALLFLAGS)
AC_SUBST(CFLAGS)
AC_SUBST(CXXFLAGS)
AC_SUBST(LDFLAGS)
AC_SUBST(LDLIBS)

AC_OUTPUT([
edelib/Version.h
doc/Doxyfile
Jamconfig
edelib.pc
edelib-dbus.pc
])

echo ""
echo "Now run 'jam' or 'jam help' to see further options."
echo ""
