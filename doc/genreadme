#!/usr/bin/gawk -f
# creates README from index.dox

function strip(str)
{
	sub(/^[ \t]+/, "", str)
	sub(/[ \t]+$/, "", str)
	return str
}

function shouldskip(ln)
{
	# doxygen start-end tags
	if((ln ~ /\*\!/) || (ln ~ /\*\//))
		return 1
	# comments
	if(ln ~ /^[ \t]*\/\//)
		return 1
	# code-endcode
	if((ln ~ /\\\code/) || (ln ~ /\\\endcode/))
		return 1
	return 0
}

function makeline(str,     ss, ln)
{
	ln = length(str)
	ss = ""
	for(i = 1; i < ln; i++)
		ss = ss"-"
	return ss
}

function parseline(ln) 
{
	if(shouldskip(ln))
		return

	if(ln ~ /\\\section/) 
	{
		sub(/\\\section/, "", ln)
		print strip(ln)
		# make '---' line exacts size of 'ln'
		print makeline(ln)
	}
	else 
	{
		# remove tags
		gsub(/<[bi]>/, "", ln)
		gsub(/<\/[bi]>/, "", ln)

		# <a href="link">name</a> -> name (link)
		# gensub returns a copy, so copy is
		# everything modified till now
		print gensub(/<a href="(.*?)">(.*?)<\/a>/, "\\2 (\\1)", "", ln)
	}
}

BEGIN {
	if(ARGC != 2) 
	{
		print "Usage: genreadme /path/index.dox"
		exit(1)
	}

	while(getline ln < ARGV[1])
		parseline(ln)
}
